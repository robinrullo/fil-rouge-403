plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'checkstyle'
}

ext {
    set('testcontainersVersion', "1.17.3")
    set('geotoolsVersion', "27.0")
}

group = 'com.rullo'
sourceCompatibility = '17'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

checkstyle {
    toolVersion '7.8.1'
    configFile file("checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
    implementation 'org.flywaydb:flyway-core'

    // Hibernate Spatial
    implementation 'org.hibernate:hibernate-spatial:5.6.9.Final'

    // GeoTools
    implementation "org.geotools.jdbc:gt-jdbc-postgis:22.2"
    implementation "org.geotools:gt-main:${geotoolsVersion}"
    implementation "org.geotools:gt-referencing:${geotoolsVersion}"
    implementation "org.geotools:gt-epsg-hsql:${geotoolsVersion}"
    implementation "org.geotools:gt-epsg-extension:${geotoolsVersion}"
    implementation "org.geotools:gt-geojson:${geotoolsVersion}"
    implementation "org.geotools:gt-wps:${geotoolsVersion}"

    implementation 'com.graphhopper.external:jackson-datatype-jts:1.0-2.7'

    // GeoJSON Serialization
    implementation 'de.grundid.opendatalab:geojson-jackson:1.14'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    mainClass = 'com.rullo.mulhousetransportrouting.MulhouseTransportRoutingApplication'
}

jar {
    enabled = false
}
task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

build.dependsOn installLocalGitHook
